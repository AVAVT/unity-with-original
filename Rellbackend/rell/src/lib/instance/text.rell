entity instance_property_value_text {
	key instance_property;
	mutable value: text;
}

/* 
 * Get & Set functions
 */
 
function set_property_text(instance, path: text, value: text) {
	_set_property_text(_instance_property_of_path(instance, path), value);
}
 
function _set_property_text(instance_property, value: text) {
	require_instance_property_not_readonly(instance_property);
	val property_value = instance_property_value_text @? { instance_property };
	
	if (property_value??) {
		property_value.value = value;
	} else {
		create instance_property_value_text(
			instance_property,
			value
		);
	}
}

function get_property_text(instance, path: text): text? {
	return _get_property_text(_instance_property_of_path(instance, path));
}

function _get_property_text(instance_property): text? {
	require(
		instance_property_type(instance_property) == property_type.text
	);
	
	return instance_property_value_text @? { instance_property }?.value;
}

function _unset_instance_property_text(instance_property) {	
	delete instance_property_value_text @? {
		instance_property
	};
}

/*
 * Query functions
 */

function find_one_text(structure, property_name: text, value: text): instance? {
	val instances = instance_property_value_text @* {
		.instance_property.instance.structure == structure,
		.instance_property.structure_property.name == property_name,
		.value == value
	} ( .instance_property.instance ) limit 1;
	
	return if (instances.size() > 0) instances[0] else null;
}

function find_all_text(structure, property_name: text, value: text): list<instance> {
	return instance_property_value_text @* {
		.instance_property.instance.structure == structure,
		.instance_property.structure_property.name == property_name,
		.value == value
	} ( .instance_property.instance );
}

/*
 * Initialization
 */

function _initialize_text_property(instance_property, init_value: gtv?) {
	val prop = instance_property.structure_property;
	
	val init_value_text = 
		if (init_value??) 
			text.from_gtv(init_value) 
		else 
			null;
	
	val default_value = 
		if (instance_property.structure_property.value.size() != 0) 
			text.from_gtv(gtv.from_bytes(instance_property.structure_property.value)) 
		else 
			null;

	require(
		default_value != null or init_value_text != null or prop.optional == true,
		"Non-optional text property '" + prop.name + "' is not initialized"
	); 
	
	require(
		prop.readonly == false
			or
		((default_value == null or init_value_text == null) and prop.readonly == true),
		"Readonly text property '" + prop.name + "' can be initialized only once"
	);
	
	val v = init_value_text ?: default_value;
	
	if (v??) {
		create instance_property_value_text(
			instance_property,
			v
		);
	}
}
