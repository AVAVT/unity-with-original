/* instance properties */

function _create_structure_property_text(structure, name, prop): structure_property {
	val txt_props = txt.from_gtv(prop.prop_type_struct.type_gtv);
	
	val v = if (txt_props.value != null) txt_props.value!!.to_gtv().to_bytes() else x"";
	
	val property = create structure_property (
		name,
		structure,
		readonly = prop.readonly.value,
		optional = prop.optional.value,
		type = property_type.text,
		value = v
	);
		
	_create_structure_property_meta_tags(property, prop.meta.tags);
	
	return property;
}

/* static properties */

entity static_property_value_text {
	key static_property;
	mutable value: text;
}

function set_static_property_text(structure, property_name: name, value: text) {
	_set_static_property_text(_static_property(structure, property_name), value);
}

function _set_static_property_text(static_property, value: text) {
	val property_value = static_property_value_text @? { static_property };

	if (property_value??) {
		property_value.value = value;
	} else {
		create static_property_value_text(
			static_property,
			value
		);
	}
}

function get_static_property_text(structure, name): text? {
	return _get_static_property_text(_static_property(structure, name));
}

function _get_static_property_text(static_property): text?{
	require(static_property.type == property_type.text);
	return static_property_value_text @? { 
		static_property
	}.value;
}

function unset_property_s_text(static_property) {
	delete static_property_value_text @ { static_property };
}

function create_static_property_text(
	structure, 
	name, 
	prop, 
	init_value: gtv?
): static_property {
	val property = create static_property(
		structure,
		name = name,
		readonly = prop.readonly.value,
		optional = prop.optional.value,
		type = property_type.text
	);
	
	_create_static_property_meta_tags(property, prop.meta.tags);
	
	val txt_def = txt.from_gtv(prop.prop_type_struct.type_gtv);
	
	val value = txt_def.value;
	val init_value_text = if (init_value??) text.from_gtv(init_value) else null;
	
	require(
		value != null or init_value_text != null or prop.optional.value == true,
		"Non-optional text property '" + name + "' is not initialized"
	); 
	
	require(
		prop.readonly.value == false
			or
		((value == null or init_value_text == null) and prop.readonly.value == true),
		"Read-only text property '" + name + "' can be initialized only once"
	);
	
	val v = init_value_text ?: value;
	
	if (v??) {
		create static_property_value_text(
			property,
			v
		);
	}
	
	return property;
}